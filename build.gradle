
buildscript {
    ext.dependency = [
        bintray: "${bintrayPluginLib}:${bintrayPluginVersion}",
        cobertura: "${coberturaPluginLib}:${coberturaVersion}",
        coveralls: "${coverallsPluginLib}:${coverallsVersion}",
        gmock: "${gmockLib}:${gmockVersion}",
        junit: "${junitLib}:${junitVersion}"
    ]

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath(
            dependency.bintray,
            dependency.cobertura,
            dependency.coveralls
        )
	classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.gradle.plugin-publish"

cobertura {
    coverageFormats = ['xml']
    coverageSourceDirs = sourceSets.main.groovy.srcDirs
}

repositories {
    mavenCentral()
    jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {

    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    publications = ['Publication']

    pkg {
        repo = 'maven'
        name = "${rootProject.name}"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/barryearles/parallel-tasks-gradle-plugin.git'
        labels = ['tasks', 'concurrent', 'concurrency', 'parallel', 'gradle', 'plugin']
        publicDownloadNumbers = true

        version {
            name = "1.0.1"
            desc = "Parallel Tasks Gradle Plugin (1.0.1)"
            released = new Date()
        }
    }
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            groupId "${group}"
            artifactId 'parallel-tasks-gradle-plugin'
            version "${version}"
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile (
        dependency.gmock,
        dependency.junit
    )
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "${group}"
version = "${version}"

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

pluginBundle {
  website = 'https://github.com/barryearles/parallel-tasks-gradle-plugin'
  vcsUrl = 'https://github.com/barryearles/parallel-tasks-gradle-plugin'
  description = 'This plugin allows tasks that are defined within the same module to be executed in parallel'
  plugins {

    parallelTasksPlugin {
      id = 'parallel-tasks'
      displayName = 'Parallel Task Execution Gradle Plugin'
      tags = ['plugin', 'task', 'concurrency', 'paralleltask' ]
      version = "1.0.1"
    }
  }

  mavenCoordinates {
    groupId = "com.github.paralleltasks"
    artifactId = "parallel-tasks-gradle-plugin"
    version = "1.0.1"
  }
}
